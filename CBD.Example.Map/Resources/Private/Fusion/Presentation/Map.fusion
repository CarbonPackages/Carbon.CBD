prototype(CBD.Example.Map:Presentation.Map) < prototype(Neos.Fusion:Component) {
    content = null
    mode = 'live'
    data = Neos.Fusion:DataStructure
    coordinates = ${this.data.coordinates}
    coordinates.@process {
        calculate = ${Carbon.Array.check(value) ? CBD.Map.centerFromCoordinates(value) : null}
        createObject = ${value ? {'center': value} : null}
    }

    settings = Neos.Fusion:DataStructure {
    }

    @if.hasContent = ${this.content}

    renderer = afx`
        <div
            class={BEM.modifier('map', props.mode)}
            style='visibility:hidden'
            data-map={Json.stringify(Array.filter(Array.concat(props.coordinates, props.settings)))}
        >
            <Carbon.IncludeAssets:Collection
                assetPackage='CBD.Example.Map'
                collection={['Map.css[data-slipstream]', 'Map.js[data-slipstream defer]']}
            />
            <div class={BEM.modifier('map__canvas', props.mode)}>
                {props.content}
            </div>
            {props.inlineEditor}
        </div>
    `
}
