prototype(Carbon.CBD:Presentation.Wrapper) < prototype(Neos.Fusion:Component) {
    @propTypes {
        count = ${PropTypes.integer}
        editable = ${PropTypes.boolean}
        type = ${PropTypes.string}
        content = ${PropTypes.string}
        contentCollectionRenderer = ${PropTypes.string}
    }

    # Add here your Fusion Prototype
    type = null
    count = 0
    editable = false
    content = null
    contentCollectionRenderer = null

    @if {
        type = ${this.type}
        visible = ${this.editable || this.count}
    }
    l18n = Neos.Fusion:DataStructure {
        edit = ${Carbon.Backend.translate('edit', 'Edit', [], 'Main', 'Carbon.CBD')}
        live = ${Carbon.Backend.translate('live', 'Live view', [], 'Main', 'Carbon.CBD')}
        noContent = ${Carbon.Backend.translate('noContent', 'No content has been created yet', [], 'Main', 'Carbon.CBD')}
    }

    renderer = afx`
        <div class={BEM.modifier('carbon-cbd', {edit: !props.count})} @if.set={props.editable}>
            <Neos.Fusion:Augmenter class='carbon-cbd__live'>
                <Neos.Fusion:Renderer type={props.type} element.content={props.content} element.count={props.count} />
            </Neos.Fusion:Augmenter>
            <div
                class='carbon-cbd__edit'
                data-carbon-warning={props.l18n.noContent}
                data-__neos-insertion-anchor
            >
                {props.contentCollectionRenderer}
            </div>
            <button type='button' class='carbon-cbd__switcher'>
                <span class={BEM.modifier('carbon-cbd__text', 'edit')}>
                    {props.l18n.edit}
                </span>
                <span class={BEM.modifier('carbon-cbd__text', 'live')}>
                    {props.l18n.live}
                </span>
            </button>
            <style data-slipstream>
                {File.readFile('resource://Carbon.CBD/Private/Fusion/Presentation/Backend.css')}
            </style>
            <script data-slipstream='//body'>
                {File.readFile('resource://Carbon.CBD/Private/Fusion/Presentation/Backend.js')}
            </script>
        </div>
        <Neos.Fusion:Renderer type={props.type} element.content={props.content} element.count={props.count} @if.set={!props.editable} />
    `
}

