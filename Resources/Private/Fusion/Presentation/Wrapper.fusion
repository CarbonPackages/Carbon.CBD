prototype(Carbon.CBD:Presentation.Wrapper) < prototype(Neos.Fusion:Component) {
    @propTypes {
        count = ${PropTypes.integer}
        editable = ${PropTypes.boolean}
        type = ${PropTypes.string}
        content = ${PropTypes.string}
        contentCollectionRenderer = ${PropTypes.string}
    }

    # Add here your Fusion Prototype
    type = null
    count = 0
    # Pass additional data
    data = Neos.Fusion:DataStructure
    editable = false
    content = null
    contentCollectionRenderer = null

    @if {
        type = ${this.type}
        visible = ${this.editable || this.count}
    }
    l18n = Neos.Fusion:DataStructure {
        edit = ${Carbon.Backend.translate('edit', 'Edit', [], 'Main', 'Carbon.CBD')}
        live = ${Carbon.Backend.translate('live', 'Live view', [], 'Main', 'Carbon.CBD')}
        noContent = ${Carbon.Backend.translate('noContent', 'No content has been created yet', [], 'Main', 'Carbon.CBD')}
    }

    renderer = afx`
        <div class={BEM.modifier('carbon-cbd', {edit: !props.count})} data-type={props.type} @if.set={props.editable}>
            <Neos.Fusion:Augmenter class='carbon-cbd__live'>
                <Neos.Fusion:Renderer
                    type={props.type}
                    element.content={props.content}
                    element.count={props.count}
                    element.data={props.data}
                />
            </Neos.Fusion:Augmenter>
            <div
                class='carbon-cbd__edit'
                data-warning={props.l18n.noContent}
                data-__neos-insertion-anchor
            >
                {props.contentCollectionRenderer}
            </div>
            <button type='button' class='carbon-cbd__switcher' onClick="carbonCBDswitcher(this)">
                <span class={BEM.modifier('carbon-cbd__text', 'edit')}>
                    {props.l18n.edit}
                </span>
                <span class={BEM.modifier('carbon-cbd__text', 'live')}>
                    {props.l18n.live}
                </span>
            </button>
            <style data-slipstream>
                {File.readFile('resource://Carbon.CBD/Private/Fusion/Presentation/Backend.css')}
            </style>
        </div>
        <Neos.Fusion:Renderer
            @if.set={!props.editable}
            type={props.type}
            element.content={props.content}
            element.count={props.count}
            element.data={props.data}
        />
    `
}
